configfile: "config/config.yaml"

# resource downloads

rule download_foldseek_database:
    output:
        "utils/foldseek_clusters.tsv.gz"
    shell:
        """
        wget -O {output} https://afdb-cluster.steineggerlab.workers.dev/5-allmembers-repId-entryId-cluFlag-taxId.tsv.gz
        """

rule build_cluster_db:
    input:
        "utils/foldseek_clusters.tsv.gz",
        "utils/uniprot_sprot.fasta.gz"
    output:
        "utils/cluster_db.sqlite3"
    script:
        "scripts/build_cluster_db.py"

rule download_swissprot_fasta:
    output:
        "utils/uniprot_sprot.fasta.gz"
    shell:
        """
        wget -O {output} ftp://ftp.uniprot.org/pub/databases/uniprot/current_release/knowledgebase/complete/uniprot_sprot.fasta.gz
        """

rule download_swissprot_entries_by_activity:  # TODO need to add ability to limit size?
    output:
        "data/{activity_type}_{activity_number}.fasta"
    wildcard_constraints:
        activity_type="ec|rhea"
    shell:
        """
        wget -O {output}.gz "https://rest.uniprot.org/uniprotkb/stream?compressed=true&format=fasta&query=%28%28{wildcards.activity_type}%3A{wildcards.activity_number}%29%29+AND+%28reviewed%3Atrue%29"
        gunzip {output}.gz
        """

# clustering

rule structure_clustering:
    input:
        "data/{sample}.fasta",
        "utils/cluster_db.sqlite3"
    output:
        "data/{sample}-structure_clusters.tar.gz"
    script:
        "scripts/structure_clustering.py"

rule sequence_clustering:
    conda: 
        "envs/mmseqs2.yaml"
    input:
        "data/{sample}-structure_clusters.tar.gz"
    output:
        temp("data/{sample}-sequence_clusters.tar.gz")
    script:
        "scripts/sequence_clustering.py"

# call TreeSAPP

rule treesapp_create:
    conda:
        "envs/treesapp.yaml"
    input:
        "utils/uniprot_sprot.fasta.gz",
        "utils/uniprot_sprot.dat.gz",
        "data/{sample}-sequence_clusters.tar.gz"
    output:
        "result/{sample}-hyperpackage.tar.gz"
    script:
        "scripts/treesapp_create.py"
